#!/usr/bin/perl

# url-2-file
#
# Stores content of the URL in a file
#
# Version 0.0.4

use strict;
use warnings;

BEGIN
{
  use File::Spec;
  use File::Basename qw[];
  use Cwd 'abs_path';

  my $APP_BINPATH = File::Basename::dirname( abs_path( $0 ) );
  unshift(@INC, $APP_BINPATH);

  my @list = File::Spec->splitdir($APP_BINPATH);
  pop @list;
  push @list, qq(lib);

  my $APP_LIBPATH = join('/', @list);
  unshift(@INC, $APP_LIBPATH);
  push @INC, $APP_BINPATH . qq(/lib);
  undef $APP_LIBPATH;
  undef $APP_BINPATH;
}

use Getopt::Long qw(:config no_auto_abbrev);
use LWP::UserAgent;

use Helpers::Misc;
use Helpers::Logger;

use constant EXIT_FAILURE => 1;
use constant EXIT_SUCCESS => 0;

our $logger = undef;

{

  our $opt = {
                'TOOL_NAME'       => qq(url-2-file),
                'TOOL_DESC'       => qq(Stores content of the URL in a file),
                'TOOL_VER_MAJOR'  => 0,
                'TOOL_VER_MINOR'  => 0,
                'TOOL_VER_PATCH'  => 3,
                'url'             => undef,
                'target-file'     => undef,
                'stdout'          => 0,
                'syslog'          => 0,
                'target_mode'     => "0644",  # this is octal
                'target-mode-str' => undef,
                'polling-delay'   => 10,
                'max-retries'     => 6000,
                '404-return'      => 1,
                'append-newline'  => 0,
                'verbose'         => 0,
                'help'            => 0,
            };

  $opt->{'TOOL_FULL_ID'} = $opt->{'TOOL_NAME'} . qq( ver ) . $opt->{'TOOL_VER_MAJOR'} . qq(.) . $opt->{'TOOL_VER_MINOR'} . qq(.) . $opt->{'TOOL_VER_PATCH'};

  my $ret = GetOptions(   "url=s"           => \$opt->{'url'},
                          "target-file=s"   => \$opt->{'target-file'},
                          "polling-delay=i" => \$opt->{'polling-delay'},
                          "target-mode=s"   => \$opt->{'target-mode-str'},
                          "stdout"          => \$opt->{'stdout'},
                          "max-retries=i"   => \$opt->{'max-retries'},
                          "syslog"          => \$opt->{'syslog'},
                          "append-newline"  => \$opt->{'append-newline'},
                          "verbose"         => \$opt->{'verbose'},
                          "help"            => \$opt->{'help'},
                      );

  display_help_and_exit($opt) if ($opt->{'help'} || !$ret);

  my $loggerConfig =  {
                          'ident' => $opt->{'TOOL_NAME'},
                          'loggers' => {
                                          'stdout' => 0,
                                          'stderr' => 1,
                                       },
                      };
  $loggerConfig->{'loggers'}->{'syslog'} = {} if ($opt->{'syslog'});
  $logger = Helpers::Logger->new($loggerConfig);


  my $target_file_txt = $opt->{'target-file'}; $target_file_txt = qq(STDOUT) if ($opt->{'stdout'});

  Helpers::Misc::display_and_exit(EXIT_FAILURE, "Need a source URL"                                       ) if ( Helpers::Misc::isEmpty($opt->{'url'}));
  Helpers::Misc::display_and_exit(EXIT_FAILURE, "Only one of target file name or --stdout can be selected") if (!Helpers::Misc::isEmpty($opt->{'target-file'}) && $opt->{'stdout'});
  Helpers::Misc::display_and_exit(EXIT_FAILURE, "Need a name of the target file or --stdout")               if ( Helpers::Misc::isEmpty($opt->{'target-file'}) && !$opt->{'stdout'});

  if (!Helpers::Misc::isEmpty($opt->{'target-mode-str'}))
    {
      $opt->{'target-mode-str'} = qq(0) . $opt->{'target-mode-str'} unless ( $opt->{'target-mode-str'} =~ m/^0/);
      my $len = length($opt->{'target-mode-str'});
      Helpers::Misc::display_and_exit( EXIT_FAILURE, "Target mode must be a valid Unix octal file mode") if (!Helpers::Misc::isStringOctal($opt->{'target-mode-str'}) || (($len != 4) && ($len != 5)));
      $opt->{'target_mode'} = oct($opt->{'target-mode-str'});
    }

  my $data = undef;
  ($ret, $data) = get_url_data($opt->{'url'});
  Helpers::Misc::display_and_exit(EXIT_FAILURE, "Failed to fetch data from url %s: %s", $opt->{'url'}, $data) if (!$ret);
  Helpers::Misc::display_and_exit(EXIT_SUCCESS, "Received 404 from %s. Not writing content to %s", $opt->{'url'}, $target_file_txt) if (!defined $data);

  $data .= "\n" if ($opt->{'append-newline'});

  my $r = undef;
  ($ret, $r) = Helpers::Misc::writeFile($opt->{'target-file'}, $data, { 'mode' => $opt->{'target_mode'} });
  Helpers::Misc::display_and_exit(EXIT_FAILURE, "Failed to write data fetched from url %s to %s: %s", $opt->{'url'}, $opt->{'target-file'}, $r) if (!$ret);

  Helpers::Misc::display_and_exit(EXIT_SUCCESS, "Wrote data fetched from %s to %s", $opt->{'url'}, $target_file_txt);

  exit(EXIT_FAILURE);
}

exit(EXIT_FAILURE);

sub get_url_data {

  my $url = shift @_;

  my $l = $main::logger;
  my $result = undef;
  for(my $retry_count = 1; $retry_count <= $main::opt->{'max-retries'}; $retry_count++)
    {
      my $ua = LWP::UserAgent->new();
      $ua->agent($main::opt->{'TOOL_FULL_ID'});
      my $req = HTTP::Request->new( qq(GET) => $url );

      my $res = $ua->request($req);
      if ($res->code eq qq(404))
        {
          return (1, undef) if ($main::opt->{'404-return'} == 1)
        }
      if ($res->code ne qq(200))
        {
          $l->log("Sleeping %s seconds and making attempt number %s", $main::opt->{'polling-delay'}, $retry_count);
          sleep($main::opt->{'polling-delay'});
          next;
        }
      $result = $res->decoded_content();
      last;
    }
  return (1, $result);
}


sub enforce_valid_mode {
  my $mode = shift @_;

  return undef if (!defined $mode);

  if (defined $mode)
    {
      my $len = length($mode);
      Helpers::Misc::display_and_exit( EXIT_FAILURE, "The mode must be a valid octal number") if (!Helpers::Misc::isStringOctal($mode) || (($len != 4) && ($len != 5)));
    }
  return $mode;
}

sub display_help_and_exit
{
  my $opt = shift @_;

  foreach my $k ( keys %$opt) { $opt->{$k} = qq(undefined) if (Helpers::Misc::isEmpty($opt->{$k})); }

  printf("\n%s - %s\n\n", $opt->{'TOOL_FULL_ID'}, $opt->{"TOOL_DESC"});
  printf("Options:\n\n");
  printf("      --url=<url>           Pull data from url <url>\n");
  printf("                            Default: %s\n", $opt->{'url'});
  printf("      --target-file=<file>> Store the content of the URL in file <file>\n");
  printf("                            Default: %s\n", $opt->{'target-file'});
  printf("      --target-mode=<mode>  After writing, change file mode to <mode>\n");
  printf("                            Default: %s\n", $opt->{'target_mode'});
  printf("      --stdout              Instead of writing to file, write to STDOUT\n");
  printf("                            Default: no\n");
  printf("      --append-newline      Append a new line character to the end of the data\n");
  printf("                            Default: no\n");
  printf("      --max-retries=<count> If failed, retry at most <count> times\n");
  printf("                            Default: %s\n", $opt->{'max-retries'});
  printf("      --polling-delay=<s>   Upon failure, wait <s> seconds before retrying\n");
  printf("                            Default: %s\n", $opt->{'polling-delay'});
  printf("      --syslog              Also log messages into syslog\n");
  printf("      --help                This help\n");

  exit(EXIT_FAILURE);

}
